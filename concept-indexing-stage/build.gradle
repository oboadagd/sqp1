plugins {
    id 'java-library'
    id "io.freefair.lombok" version "5.3.0"
}

version = '0.0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://artifactory.lucidworks.com/artifactory/public-artifacts/'
    }
}

configurations {
    provided
    implementation.extendsFrom(provided)
}

dependencies {
    provided 'com.lucidworks-plugins.index-stage-sdk:index-stage-plugin-sdk:1.+'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'org.apache.commons:commons-collections4:4.0'
    implementation 'org.projectlombok:lombok:1.18.16'

    testImplementation 'com.lucidworks-plugins.index-stage-sdk:index-stage-sdk-test:1.+'
}

task assemblePlugin(type: Jar) {
    group("build")
    getArchiveExtension().set("zip")
    manifest {
        attributes 'Manifest-Version': '1.0'
        attributes 'Plugin-Id': 'concept-indexing-plugin'
        attributes 'Plugin-Version': '0.0.1'
        attributes 'Plugin-SDK-Version': '1.1.0'
        attributes 'Plugin-Base-Package': 'com.lucidworks.fusion.semanticsearch.indexing.stages'
    }

    into('lib') {
        from configurations.runtimeClasspath - configurations.provided
        from jar
    }
}
